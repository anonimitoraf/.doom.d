#+TITLE: Appearance

* Theme
#+begin_src elisp :results none
(use-package emacs
  :init
  ;; Add all your customizations prior to loading the themes
  (setq modus-themes-italic-constructs t
    modus-themes-bold-constructs nil
    modus-themes-region '(bg-only no-extend)
    modus-themes-mode-line '((padding . 1) (height . 0.2)))
  :config
  (load-theme 'modus-vivendi))
#+end_src
* Font
I want my font to automatically resize based on which screen Emacs is
#+begin_src elisp
(defvar ++font-size nil)
(defun ++screen-pixels->font-size (width-x-height)
  "Given WIDTH_X_HEIGHT, returns the adjusted font size"
  (let ((default-font-size 16))
    (cond ((member width-x-height
                   '((3440 1440))) 18)
          ((member width-x-height
                   '((1920 1080))) 14)
          ;; My Flux mac
          ((member width-x-height
                    '((1440 900))) 16)
          (t (progn
               (message (concat "Unhandled screen resolution " (prin1-to-string width-x-height) ". "
                                "Defaulting to font size " (prin1-to-string default-font-size)))
               default-font-size)))))

;; Stolen from https://github.com/hlissner/doom-emacs/issues/1500
(defun ++get-frame-list (&optional frame)
  "Return a list consisting of FRAME and all of FRAME's child frames."
  (let ((frame (or frame (selected-frame))))
    (cons (selected-frame)
          (cl-loop for fr in (frame-list)
                   if (eq (frame-parameter fr 'parent-frame) frame)
                   collect fr))))

(defun ++configure-font-size ()
  (let ((new-font-size (++screen-pixels->font-size
                        (cddr (frame-monitor-attribute 'geometry)))))
    (unless (equal new-font-size ++font-size)
      (setq doom-font (font-spec :family "Ubuntu Mono" :size new-font-size))
      (set-frame-font doom-font t (++get-frame-list)))
    (setq ++font-size new-font-size)))

(when (display-graphic-p)
 (run-at-time 0 nil (lambda () (++configure-font-size)))
 (setq ++adjust-font-timer (run-with-idle-timer 1 1 #'++configure-font-size)))
#+end_src
* Dashboard
** Banner
#+begin_src elisp
(defun ++ascii-banner-ansi-shadow ()
  (mapc (lambda (line)
          (insert (propertize (+doom-dashboard--center +doom-dashboard--width line)
                              'face 'doom-dashboard-banner) " ")
          (insert "\n"))
        '("=================     ===============     ===============   ========  ========"
          "\\\\ . . . . . . .\\\\   //. . . . . . .\\\\   //. . . . . . .\\\\  \\\\. . .\\\\// . . //"
          "||. . ._____. . .|| ||. . ._____. . .|| ||. . ._____. . .|| || . . .\\/ . . .||"
          "|| . .||   ||. . || || . .||   ||. . || || . .||   ||. . || ||. . . . . . . ||"
          "||. . ||   || . .|| ||. . ||   || . .|| ||. . ||   || . .|| || . | . . . . .||"
          "|| . .||   ||. _-|| ||-_ .||   ||. . || || . .||   ||. _-|| ||-_.|\\ . . . . ||"
          "||. . ||   ||-'  || ||  `-||   || . .|| ||. . ||   ||-'  || ||  `|\\_ . .|. .||"
          "|| . _||   ||    || ||    ||   ||_ . || || . _||   ||    || ||   |\\ `-_/| . ||"
          "||_-' ||  .|/    || ||    \\|.  || `-_|| ||_-' ||  .|/    || ||   | \\  / |-_.||"
          "||    ||_-'      || ||      `-_||    || ||    ||_-'      || ||   | \\  / |  `||"
          "||    `'         || ||         `'    || ||    `'         || ||   | \\  / |   ||"
          "||            .===' `===.         .==='.`===.         .===' /==. |  \\/  |   ||"
          "||         .=='   ███████╗███╗   ███╗ █████╗  ██████╗███████╗  `==  \\/  |   ||"
          "||      .=='    _-██╔════╝████╗ ████║██╔══██╗██╔════╝██╔════╝_  /|  \\/  |   ||"
          "||   .=='    _-'  █████╗  ██╔████╔██║███████║██║     ███████╗ `' |. /|  |   ||"
          "||.=='    _-'     ██╔══╝  ██║╚██╔╝██║██╔══██║██║     ╚════██║     `' |  /==.||"
          "=='    _-'        ███████╗██║ ╚═╝ ██║██║  ██║╚██████╗███████║         \\/   `=="
          "\\   _-'           ╚══════╝╚═╝     ╚═╝╚═╝  ╚═╝ ╚═════╝╚══════╝          `-_   /"
          "`''                                                                      ``'")))
(setq +doom-dashboard-ascii-banner-fn #'++ascii-banner-ansi-shadow)
#+end_src
** Remove the menu
I know most of the keyboard shortcuts by now :P
#+begin_src elisp
(remove-hook '+doom-dashboard-functions #'doom-dashboard-widget-shortmenu)
#+end_src
** Show a random Clojure core symbol (for learning purposes)
#+begin_src elisp
(require 'clojure-rand-ref)

(defun ++dashboard-trivia ()
 (clojure-rand-ref
  (lambda (entry)
    (with-current-buffer +doom-dashboard-name
      (when entry
        (read-only-mode -1)
        (goto-char (point-min))
        (forward-line 5)
        (insert "Clojure Trivia\n\n")
        (insert-text-button (concat "  " (plist-get entry :symbol) "\n")
                            'action (lambda (_)
                                      (+doom-dashboard-reload t)
                                      (++dashboard-trivia)
                                      (browse-url (plist-get entry :link)))
                            'face 'doom-dashboard-menu-title
                            'mouse-face 'doom-dashboard-menu-title
                            'follow-link t)
        (insert "  " (plist-get entry :description) "\n")
        (read-only-mode +1))))))

(unless IS-MAC (advice-add #'+doom-dashboard-init-h :after #'++dashboard-trivia))
#+end_src
* Misc
#+begin_src elisp
(setq display-time-default-load-average nil
      display-time-24hr-format t
      display-line-numbers-type 'relative)
#+end_src

Highlight the whole s-expr, not just the enclosing parens.
#+begin_src elisp
(setq show-paren-style 'expression)
#+end_src
