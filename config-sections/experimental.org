#+TITLE: Experimental

* =corfu and co.=
#+begin_src emacs-lisp :results none
(use-package corfu
  :config
  (defun ++corfu-quit ()
    (interactive)
    (call-interactively 'corfu-quit)
    (evil-normal-state +1))
  (setq corfu-min-width 25
        corfu-count 15
        corfu-cycle t
        corfu-auto t
        corfu-auto-prefix 1
        corfu-auto-delay 0.01
        corfu-separator ?\s
        corfu-quit-at-boundary 'separator
        corfu-quit-no-match t
        corfu-preview-current nil
        corfu-preselect-first t
        corfu-on-exact-match nil
        corfu-echo-documentation nil
        corfu-scroll-margin 10
        corfu-popupinfo-delay 0.1
        corfu-popupinfo-max-height 20
        corfu-popupinfo-max-width 100)
  (map! :map global-map
        :nvi "C-SPC" #'completion-at-point)
  (map! :map corfu-map
        :nvi "C-j" #'corfu-next
        :nvi "C-k" #'corfu-previous
        :nvi "C-l" #'corfu-insert
        :nvi "C-;" #'corfu-insert
        :nvi "TAB" #'corfu-insert
        :nvi "<tab>" #'corfu-insert
        :nvi "<escape>" #'++corfu-quit
        :nvi "ESC" #'++corfu-quit)
  (global-corfu-mode t)
  (corfu-popupinfo-mode t)
  (corfu-history-mode t)
  (global-company-mode -1)
  (add-hook! '(prog-mode-hook
               text-mode-hook)
    (unless (display-graphic-p)
      (corfu-terminal-mode t)
      (corfu-doc-terminal-mode t))))

(use-package kind-icon
  :after corfu
  :custom
  (kind-icon-default-face 'corfu-default) ; to compute blended backgrounds correctly
  :config
  (add-to-list 'corfu-margin-formatters #'kind-icon-margin-formatter))

;; See https://github.com/minad/corfu/wiki#basic-example-configuration-with-orderless
(use-package orderless
  :init
  ;; Tune the global completion style settings to your liking!
  ;; This affects the minibuffer and non-lsp completion at point.
  (setq completion-styles '(orderless partial-completion basic)
        completion-category-defaults nil
        completion-category-overrides nil))

(use-package lsp-mode
  :custom
  (lsp-completion-provider :none) ;; we use Corfu!
  :init
  (defun my/lsp-mode-setup-completion ()
    (setf (alist-get 'styles (alist-get 'lsp-capf completion-category-defaults))
          '(orderless))) ;; Configure orderless
  :hook
  (lsp-completion-mode . my/lsp-mode-setup-completion))

;; Add extensions
(use-package cape
  :init
  ;; Add `completion-at-point-functions', used by `completion-at-point'.
  (add-to-list 'completion-at-point-functions #'cape-file)
  ;;(add-to-list 'completion-at-point-functions #'cape-tex)
  (add-to-list 'completion-at-point-functions #'cape-dabbrev)
  (add-to-list 'completion-at-point-functions #'cape-keyword)
  ;;(add-to-list 'completion-at-point-functions #'cape-symbol)
  ;;(add-to-list 'completion-at-point-functions #'cape-sgml)
  ;;(add-to-list 'completion-at-point-functions #'cape-rfc1345)
  ;;(add-to-list 'completion-at-point-functions #'cape-abbrev)
  ;;(add-to-list 'completion-at-point-functions #'cape-ispell)
  ;;(add-to-list 'completion-at-point-functions #'cape-dict)
  ;;(add-to-list 'completion-at-point-functions #'cape-line)
  :config
  (setq cape-dabbrev-min-length 2
        cape-dabbrev-check-other-buffers 'some))
#+end_src

Scroll-bar appearance
#+begin_src elisp :results none
(setq corfu-bar-width 0.5)
(custom-set-faces! `(corfu-bar :background ,(doom-color 'magenta)))
#+end_src
* =eat=
#+begin_src elisp :results none
(use-package! eat
  :init
  (setq eat-term-terminfo-directory (expand-file-name ".local/straight/repos/emacs-eat/terminfo" doom-emacs-dir)
        eat-term-shell-integration-directory (expand-file-name ".local/straight/repos/emacs-eat/integration" doom-emacs-dir)))
#+end_src

* =google-translate=
Japanese kana -> romaji
#+begin_src emacs-lisp :tangle yes :results none
(defvar ++google-translate-kana->romaji-buffer "*Google Translate kana->romaji*")

(defun ++google-translate-kana->romaji (kana)
  (replace-regexp-in-string "\r?\n" ""
    (shell-command-to-string
     (format "echo \"%s\" | iconv -f utf8 -t eucjp | kakasi -i euc -w | kakasi -i euc -Ha -Ka -Ja -Ea -ka" kana))))

(defun ++google-translate-curr-region-kana->romaji ()
  (when (equal (buffer-name) "*Google Translate*")
    (if (region-active-p)
      (posframe-show ++google-translate-kana->romaji-buffer
        :string (++google-translate-kana->romaji (buffer-substring-no-properties
                                                   (region-beginning)
                                                   (+ (region-end) 1)))
        :position (point)
        :border-color (doom-color 'yellow)
        :border-width 1
        :foreground-color (doom-color 'yellow))
      (posframe-hide ++google-translate-kana->romaji-buffer))))

(defun ++google-translate-setup-hook ()
  (add-hook 'post-command-hook #'++google-translate-curr-region-kana->romaji))

(++google-translate-setup-hook)
#+end_src
* =difftastic=
Borrowed https://tsdh.org/posts/2022-08-01-difftastic-diffing-with-magit.html
#+begin_src elisp :results none
(use-package! magit
  :config
  (defun th/magit--with-difftastic (buffer command)
    "Run COMMAND with GIT_EXTERNAL_DIFF=difft then show result in BUFFER."
    (let ((process-environment
           (cons (concat "GIT_EXTERNAL_DIFF=difft --width="
                         (number-to-string (frame-width)))
                 process-environment)))
      ;; Clear the result buffer (we might regenerate a diff, e.g., for
      ;; the current changes in our working directory).
      (with-current-buffer buffer
        (setq buffer-read-only nil)
        (erase-buffer))
      ;; Now spawn a process calling the git COMMAND.
      (make-process
       :name (buffer-name buffer)
       :buffer buffer
       :command command
       ;; Don't query for running processes when emacs is quit.
       :noquery t
       ;; Show the result buffer once the process has finished.
       :sentinel (lambda (proc event)
                   (when (eq (process-status proc) 'exit)
                     (with-current-buffer (process-buffer proc)
                       (goto-char (point-min))
                       (ansi-color-apply-on-region (point-min) (point-max))
                       (setq buffer-read-only t)
                       (view-mode)
                       (end-of-line)
                       ;; difftastic diffs are usually 2-column side-by-side,
                       ;; so ensure our window is wide enough.
                       (let ((width (current-column)))
                         (while (zerop (forward-line 1))
                           (end-of-line)
                           (setq width (max (current-column) width)))
                         ;; Add column size of fringes
                         (setq width (+ width
                                        (fringe-columns 'left)
                                        (fringe-columns 'right)))
                         (goto-char (point-min))
                         (pop-to-buffer
                          (current-buffer)
                          `(;; If the buffer is that wide that splitting the frame in
                            ;; two side-by-side windows would result in less than
                            ;; 80 columns left, ensure it's shown at the bottom.
                            ,(when (> 80 (- (frame-width) width))
                               #'display-buffer-at-bottom)
                            (window-width
                             . ,(min width (frame-width))))))))))))

  (defun th/magit-show-with-difftastic (rev)
    "Show the result of \"git show REV\" with GIT_EXTERNAL_DIFF=difft."
    (interactive
     (list (or
            ;; If REV is given, just use it.
            (when (boundp 'rev) rev)
            ;; If not invoked with prefix arg, try to guess the REV from
            ;; point's position.
            (and (not current-prefix-arg)
                 (or (magit-thing-at-point 'git-revision t)
                     (magit-branch-or-commit-at-point)))
            ;; Otherwise, query the user.
            (magit-read-branch-or-commit "Revision"))))
    (if (not rev)
        (error "No revision specified")
      (th/magit--with-difftastic
       (get-buffer-create (concat "*git show difftastic " rev "*"))
       (list "git" "--no-pager" "show" "--ext-diff" rev))))

  (defun th/magit-diff-with-difftastic (arg)
    "Show the result of \"git diff ARG\" with GIT_EXTERNAL_DIFF=difft."
    (interactive
     (list (or
            ;; If RANGE is given, just use it.
            (when (boundp 'range) range)
            ;; If prefix arg is given, query the user.
            (and current-prefix-arg
                 (magit-diff-read-range-or-commit "Range"))
            ;; Otherwise, auto-guess based on position of point, e.g., based on
            ;; if we are in the Staged or Unstaged section.
            (pcase (magit-diff--dwim)
              ('unmerged (error "unmerged is not yet implemented"))
              ('unstaged nil)
              ('staged "--cached")
              (`(stash . ,value) (error "stash is not yet implemented"))
              (`(commit . ,value) (format "%s^..%s" value value))
              ((and range (pred stringp)) range)
              (_ (magit-diff-read-range-or-commit "Range/Commit"))))))
    (let ((name (concat "*git diff difftastic"
                        (if arg (concat " " arg) "")
                        "*")))
      (th/magit--with-difftastic
       (get-buffer-create name)
       `("git" "--no-pager" "diff" "--ext-diff" ,@(when arg (list arg))))))

  (transient-define-prefix th/magit-aux-commands ()
    "My personal auxiliary magit commands."
    ["Auxiliary commands"
     ("d" "Difftastic Diff (dwim)" th/magit-diff-with-difftastic)
     ("s" "Difftastic Show" th/magit-show-with-difftastic)])

  (transient-append-suffix 'magit-dispatch "!"
    '("#" "My Magit Cmds" th/magit-aux-commands))

  (define-key magit-status-mode-map (kbd "#") #'th/magit-aux-commands))
#+end_src
* =tree-sitter=
Set up languages
#+begin_src elisp :results none
(setq treesit-language-source-alist
   '((bash "https://github.com/tree-sitter/tree-sitter-bash")
     (cmake "https://github.com/uyha/tree-sitter-cmake")
     (css "https://github.com/tree-sitter/tree-sitter-css")
     (elisp "https://github.com/Wilfred/tree-sitter-elisp")
     (go "https://github.com/tree-sitter/tree-sitter-go")
     (html "https://github.com/tree-sitter/tree-sitter-html")
     (javascript "https://github.com/tree-sitter/tree-sitter-javascript" "master" "src")
     (json "https://github.com/tree-sitter/tree-sitter-json")
     (make "https://github.com/alemuller/tree-sitter-make")
     (markdown "https://github.com/ikatyang/tree-sitter-markdown")
     (python "https://github.com/tree-sitter/tree-sitter-python")
     (toml "https://github.com/tree-sitter/tree-sitter-toml")
     (tsx "https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src")
     (typescript "https://github.com/tree-sitter/tree-sitter-typescript" "master" "typescript/src")
     (yaml "https://github.com/ikatyang/tree-sitter-yaml")))

(setq tree-sitter-major-mode-language-alist
      '((tsx-ts-mode . tsx)
        (typescript-ts-mode . typescript)))
#+end_src

Evaluate to install grammars
#+begin_src elisp :tangle no
(++comment
 (mapc #'treesit-install-language-grammar (mapcar #'car treesit-language-source-alist))
)
#+end_src


Auto-enable for particular modes
#+begin_src elisp :results none
(add-hook! '(typescript-ts-mode-hook
             tsx-ts-mode-hook)
           #'tree-sitter-mode)
#+end_src
Enable LSP
#+begin_src elisp :results none
(add-hook! '(typescript-ts-mode-hook
             tsx-ts-mode-hook)
           #'lsp)
#+end_src
* =tsx-ts-helper-mode=
#+begin_src elisp :results none
(use-package tsx-ts-helper-mode
  :commands (tsx-ts-helper-mode)
  :hook (tsx-ts-mode . tsx-ts-helper-mode)
  :config
  (map! :map tsx-ts-mode-map
        :nv "SPC m r" #'tsx-ts-helper-mode-rename-tag
        :nv "SPC m d" #'tsx-ts-helper-mode-delete-all))
#+end_src
* =cider-storm=
So I don't accidentally edit the buffer, make it read-only while debugging
#+begin_src elisp :results none
(use-package cider-storm
  :config
  (add-hook 'cider-storm-debugging-mode-hook
            (lambda () (read-only-mode t)))
  (advice-add #'cider-storm--debug-mode-quit :after
              (lambda () (read-only-mode -1))))
#+end_src
Make the overlay stand out
#+begin_src elisp :results none
(custom-set-faces!
  `(cider-debug-code-overlay-face :background ,++dark-red))
#+end_src
